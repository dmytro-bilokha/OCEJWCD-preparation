//noinspection GroovyAssignabilityCheck

apply plugin: 'java'
apply plugin: 'war'

war {
    baseName = 'showsession'
    version = '0.0.1'
    classifier = 'SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.bmuschko:gradle-cargo-plugin:2.2.2"
    }
}

apply plugin: "com.bmuschko.cargo"

repositories {
    mavenCentral()
}

configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.4'
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

sourceSets {
    itest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

task itest(type: Test) {
    description = 'Runs the integration tests'
    group = 'verification'
    testClassesDir = sourceSets.itest.output.classesDir
    classpath = sourceSets.itest.runtimeClasspath
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

cargo {
    containerId = 'tomcat8x'
    deployable {
        file = file("${war.archivePath}")
        context = "${war.baseName}"
    }
    remote {
        hostname = "localhost"
        username = "tomcat"
        password = "tomcat"
        timeout = 6000
    }
}

cargoRedeployRemote.dependsOn build
